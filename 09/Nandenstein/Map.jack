class Map {
  // multi-dimension array ([row][column]) because multiplication is expensive
  field Array map;
  // LUT for delta distance values
  field Array dd_lut;
  // LUT for angle changes between x screen columns
  field Array x_angle_lut_hi;
  field Array x_angle_lut_lo;
  // LUT for height from distance
  field Array height_from_dist_lut;

  field int delta_dist_x, delta_dist_y;
  field int side_dist_x, side_dist_y;

  field int w, h;

  constructor Map new(int Width, int Height) {
    var int row, col;
    var Array cur_row;
    let w = Width;
    let h = Height;

    let map = Array.new(h);
    let row = 0;
    while (row < h) {
      let map[row] = Array.new(w);
      let cur_row = map[row];

      let col = 0;
      while (col < w) {
        if (((col = row) & (col & 1)) | (col = 0) | (row = 0) | (col = (w - 1)) | (row = (h - 1))) {
          let cur_row[col] = 1;
        } else {
          let cur_row[col] = 0;
        }
        let col = col + 1;
      }
      let row = row + 1;
    }

    let dd_lut = Array.new(256);
    do Luts.init_delta_dist(dd_lut);

    let x_angle_lut_hi = Array.new(32);
    let x_angle_lut_lo = Array.new(32);
    do Luts.init_x_angle(x_angle_lut_hi, x_angle_lut_lo);

    let height_from_dist_lut = Array.new(256);
    do Luts.init_height_from_dist(height_from_dist_lut);

    return this;
  }

  method void dispose() {
    do map.dispose();
    do dd_lut.dispose();
    do x_angle_lut_hi.dispose();
    do x_angle_lut_lo.dispose();
    do height_from_dist_lut.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method int at(int x, int y) {
    var Array cur_row;
    let cur_row = map[y];
    return cur_row[x];
  }

  method void print() {
    var int row, col;
    var Array cur_row;

    let row = 0;
    while (row < h) {
      let cur_row = map[row];

      let col = 0;
      while (col < w) {
        do Output.printInt(cur_row[col]);
        do Output.printChar(20);

        let col = col + 1;
      }
      do Output.println();
      let row = row + 1;
    }

    return;
  }

  // 0 <= dir < 256
  // depth_array.len = 32
  method void trace_screen(Fixed pos_x, Fixed pos_y, Fixed camera_dir, Array depth_array) {
    var Fixed theta;
    var int screen_x;
    // copy camera_dir so we don't screw it up
    // the -32 here assumes a 90 degree FOV
    let theta = Fixed.new(camera_dir[0] - 32, camera_dir[1]);
    // using [0] for .hi and [1] for .lo saves us some slow function calls
    if (theta[0] < 0) {
      let theta[0] = theta[0] + 256;
    }
    let screen_x = 31;
    while (screen_x > -1) {
      // Increment the angle
      do theta.Add(x_angle_lut_hi[screen_x], x_angle_lut_lo[screen_x]);
      if (theta[0] > 255) {
        let theta[0] = theta[0] - 256;
      }
      let depth_array[screen_x] = trace(pos_x, pos_y, theta);
      let screen_x = screen_x - 1;
    }
    do theta.dispose();
    return;
  }

  method int trace(Fixed pos_x, Fixed pos_y, Fixed dir) {
    var boolean hit;
    var boolean side;
    var Array cur_row;
    var int map_x, map_y;
    var int step_x, step_y;
    var int dir_x, dir_y;  // dir_y is just dir_x rotated 90 degrees
    var int dist;
    var int dist_shift;
    var int bitmask, addend;

    let hit = false;

    let map_x = pos_x[0];
    let map_y = pos_y[0];

    let dir_x = dir[0];
    let dir_y = dir[0] - 64;
    if (dir_y < 0) {
      let dir_y = dir_y + 256;
    }

    let delta_dist_x = dd_lut[dir_x];
    let delta_dist_y = dd_lut[dir_y];

    if ((dir[0] < 64) | (dir[0] > 192)) {
      let step_x = 1;
      let side_dist_x = delta_dist_x;
    } else {
      let step_x = -1;
      let side_dist_x = 0;
    }
    if (dir[0] > 128) {
      let step_y = 1;
      let side_dist_y = delta_dist_y;
    } else {
      let step_y = -1;
      let side_dist_y = 0;
    }

    while (~hit) {
      if (side_dist_x < side_dist_y) {
        let side_dist_x = side_dist_x + delta_dist_x;
        let map_x = map_x + step_x;
        let side = true;
      } else {
        let side_dist_y = side_dist_y + delta_dist_y;
        let map_y = map_y + step_y;
        let side = false;
      }
      let cur_row = map[map_y];
      if (cur_row[map_x]) {
        let hit = true;
      }
    }

    if (side) {
      let dist = side_dist_x;
    } else {
      let dist = side_dist_y;
    }

    let bitmask = 32;
    let addend = 1;
    let dist_shift = 0;

    while (addend < 256) {
      if (dist & bitmask) {
        let dist_shift = dist_shift + addend;
      }
      let bitmask = bitmask + bitmask;
      let addend = addend + addend;
    }

    if (side) {
      return height_from_dist_lut[dist_shift];
    } else {
      return -height_from_dist_lut[dist_shift];
    }
  }
}

// vim: set cindent:
